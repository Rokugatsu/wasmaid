toolchain("clang") {
  tool("cc") {
    depfile = "{{output}}.d"
    depsformat = "gcc"

    command = "clang -MMD -MF $depfile {{defines}} {{include_dirs}} {{cflags}} {{cflags_c}} -c {{source}} -o {{output}}"
    description = "CC {{output}}"

    outputs =
        [ "{{source_out_dir}}/{{target_output_name}}.{{source_name_part}}.o" ]
  }

  tool("cxx") {
    depfile = "{{output}}.d"
    depsformat = "gcc"

    command = "clang++ -MMD -MF $depfile {{defines}} {{include_dirs}} {{cflags}} {{cflags_cc}} -c {{source}} -o {{output}}"
    description = "CXX {{output}}"

    outputs =
        [ "{{source_out_dir}}/{{target_output_name}}.{{source_name_part}}.o" ]
  }

  tool("alink") {
    output_prefix = "lib"
    default_output_extension = ".a"

    command = "rm -f {{output}} && ar rcs {{output}} {{inputs}}"
    description = "AR {{target_output_name}}{{output_extension}}"

    outputs =
        [ "{{target_out_dir}}/{{target_output_name}}{{output_extension}}" ]
  }

  tool("solink") {
    # Use this for {{output_dir}} expansions unless a target manually overrides
    # it (in which case {{output_dir}} will be what the target specifies).

    default_output_dir = "{{root_out_dir}}"
    output_prefix = "lib"

    # Use this for {{output_extension}} expansions unless a target manually
    # overrides it (in which case {{output_extension}} will be what the target
    # specifies).
    default_output_extension = ".dylib"

    soname = "{{target_output_name}}{{output_extension}}"
    sofile = "{{output_dir}}/$soname"

    # On some systems (such as older UNIX systems and certain Windows variants) command-lines have relatively limited lengths.
    # Windows compilers therefore support "response files". These files are mentioned on the command-line (using the "@file") syntax.
    # The compiler reads these files and inserts the contents into argv, thereby working around the command-line length limits.
    # This feature is frequently requested by Windows users of GCC.
    rspfile = soname + ".rsp"
    rspfile_content = "{{inputs}} {{solibs}} {{libs}}"

    os_specific_option = "-install_name @rpath/$sofile"

    command =
        "clang++ -shared {{ldflags}} -o $sofile $os_specific_option @$rspfile"
    description = "SOLINK $soname"

    outputs = [ sofile ]
  }

  tool("link") {
    default_output_dir = "{{root_out_dir}}"
    outfile = "{{target_output_name}}{{output_extension}}"

    rspfile = "$outfile.rsp"
    rspfile_content = "{{inputs}}"

    command = "clang++ {{ldflags}} -o $outfile @$rspfile {{solibs}} {{libs}}"
    description = "LINK $outfile"

    outputs = [ outfile ]
  }

  tool("stamp") {
    command = "touch {{output}}"
    description = "STAMP {{output}}"
  }

  tool("copy") {
    command = "cp -af {{source}} {{output}}"
    description = "COPY {{source}} {{output}}"
  }
}
